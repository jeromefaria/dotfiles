# ============================================================================
# Platform Detection
# ============================================================================
# Detect the current platform for conditional configuration loading
# Supported: macos, linux, windows (WSL/Cygwin)

DOTFILES_PLATFORM="unknown"
case "$(uname -s)" in
  Darwin)
    DOTFILES_PLATFORM="macos"
    ;;
  Linux)
    # Check for WSL
    if grep -qEi "(Microsoft|WSL)" /proc/version 2>/dev/null; then
      DOTFILES_PLATFORM="wsl"
    else
      DOTFILES_PLATFORM="linux"
    fi
    ;;
  MINGW*|MSYS*|CYGWIN*)
    DOTFILES_PLATFORM="windows"
    ;;
esac
export DOTFILES_PLATFORM

# ============================================================================
# Homebrew (must be early for PATH setup)
# ============================================================================
if [[ "$DOTFILES_PLATFORM" == "macos" ]]; then
  if [[ -f "/opt/homebrew/bin/brew" ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
  elif [[ -f "/usr/local/bin/brew" ]]; then
    eval "$(/usr/local/bin/brew shellenv)"
  fi
fi

# ============================================================================
# rbenv shims (must be in PATH for non-interactive tools like neovim/mason)
# ============================================================================
[[ -d "$HOME/.rbenv/shims" ]] && export PATH="$HOME/.rbenv/shims:$PATH"

# ============================================================================
# Platform-Specific: macOS
# ============================================================================
if [[ "$DOTFILES_PLATFORM" == "macos" ]]; then
  # Volume shortcuts
  export ARCHIVE="/Volumes/Archive"
  export AUDIO="/Volumes/Audio"
  export MEDIA="/Volumes/Media"
  export PORTABLE="/Volumes/Portable"
  export VIDEO="/Volumes/Video"
  export CINEMA="$MEDIA/Cinema"
  export DRIVE="$ARCHIVE/Drive"
  export MOBILE="$HOME/Library/Mobile\ Documents"
  export MUSIC="$MEDIA/Music/FLAC"
  export PROJECTS="$AUDIO/Audio/Projects"
  export SHARED="$DRIVE/Shared"
  export TELEVISION="$MEDIA/Television"

  # Day One CLI
  export DAYONE_APP_PATH="/Applications/Day One.app"

  # cyrus-sasl for neomutt
  [[ -d "/opt/homebrew/opt/cyrus-sasl/sbin" ]] && \
    export PATH="/opt/homebrew/opt/cyrus-sasl/sbin:$PATH"

  # Broot launcher
  [[ -f "$HOME/Library/Preferences/org.dystroy.broot/launcher/bash/br" ]] && \
    source "$HOME/Library/Preferences/org.dystroy.broot/launcher/bash/br"
fi

# ============================================================================
# Cross-Platform Folders
# ============================================================================
export DOTFILES="$HOME/dotfiles"
export DOWNLOADS="$HOME/Downloads"
export WORK="$HOME/Work"

# ZSH settings
ZSH=$HOME/.oh-my-zsh
ZSH_THEME=""

# ZSH plugins
plugins=(git macos encode64 web-search last-working-dir git-flow-completion zsh-syntax-highlighting zsh-autosuggestions git-open brew omz-git zsh-vi-mode)

# zsh-vi-mode settings (must be set before loading oh-my-zsh)
export ZVM_VI_ESCAPE_BINDKEY=jk
ZVM_CURSOR_STYLE_ENABLED=false

# FZF - must be loaded before zsh-interactive-cd plugin
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# ZSH INTERACTIVE CD (requires fzf)
[[ -f "$ZSH/plugins/zsh-interactive-cd/zsh-interactive-cd.plugin.zsh" ]] && \
  source "$ZSH/plugins/zsh-interactive-cd/zsh-interactive-cd.plugin.zsh"

# SOURCES
source "$ZSH/oh-my-zsh.sh"

# Load modular aliases (platform-aware)
# Skip macOS-specific files on non-macOS platforms
local -a _macos_only=(macos.sh chrome.sh folders.sh)
for alias_file in "$DOTFILES"/shell/aliases/*.sh; do
  _filename=${alias_file:t}
  if (( ${_macos_only[(Ie)$_filename]} )) && [[ "$DOTFILES_PLATFORM" != "macos" ]]; then
    continue
  fi
  source "$alias_file" || echo "Warning: Failed to load $alias_file" >&2
done

# Load modular functions (platform-aware)
for func_file in "$DOTFILES"/shell/functions/*.sh; do
  _filename=${func_file:t}
  if [[ "$_filename" == "macos.sh" && "$DOTFILES_PLATFORM" != "macos" ]]; then
    continue
  fi
  source "$func_file" || echo "Warning: Failed to load $func_file" >&2
done
unset _filename _macos_only

# EDITOR
export VISUAL=nvim
export EDITOR="$VISUAL"

# MAN PAGES
export MANPAGER='nvim +Man!'

# BINDINGS
bindkey '^ ' autosuggest-accept
bindkey -M menuselect '^[[Z' reverse-menu-complete
bindkey -M viins 'jj' vi-cmd-mode

# zsh-vi-mode post-init hooks (re-apply bindings that zvm overwrites)
zvm_after_init_commands+=('bindkey "^ " autosuggest-accept')
zvm_after_init_commands+=('[ -f /opt/homebrew/opt/fzf/shell/key-bindings.zsh ] && source /opt/homebrew/opt/fzf/shell/key-bindings.zsh')

# ============================================================================
# PATH Configuration
# ============================================================================
export PATH="$HOME/.local/bin:/usr/local/sbin:$PATH"
export PATH="/usr/local/opt/node@22/bin:$PATH"
export PATH="/usr/local/opt/ruby/bin:$PATH"

# Locale
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

fpath=("$HOME/.zsh/completions" $fpath)

# Cached compinit - only regenerate once per day
autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi

# ============================================================================
# Tool Initialization (Platform-Aware)
# ============================================================================

# Define stub functions early to prevent "command not found" errors
# These get overwritten by the real implementations below
_zoxide_lazy() { :; }
_zoxide_lazy_i() { :; }
_thefuck_lazy() { :; }
_rbenv_lazy() { :; }

# rbenv (lazy-loaded for faster shell startup)
if command -v rbenv &>/dev/null; then
  _rbenv_lazy() {
    unset -f _rbenv_lazy ruby rbenv gem irb
    eval "$(rbenv init - zsh)"
  }
  ruby()  { _rbenv_lazy; ruby "$@"; }
  rbenv() { _rbenv_lazy; command rbenv "$@"; }
  gem()   { _rbenv_lazy; gem "$@"; }
  irb()   { _rbenv_lazy; irb "$@"; }
fi

# Initialise Starship (cross-platform)
if command -v starship &>/dev/null; then
  eval "$(starship init zsh)"
fi

# Broot launcher (cross-platform fallback)
if [[ -f "$HOME/.config/broot/launcher/bash/br" ]]; then
  source "$HOME/.config/broot/launcher/bash/br"
fi

# Volta
if [[ -d "$HOME/.volta" ]]; then
  export VOLTA_HOME="$HOME/.volta"
  export PATH="$VOLTA_HOME/bin:$PATH"
fi

# thefuck (lazy-loaded for faster shell startup)
if command -v thefuck &>/dev/null; then
  _thefuck_lazy() {
    unset -f fuck _thefuck_lazy
    eval "$(thefuck --alias)"
    fuck "$@"
  }
  alias fuck='_thefuck_lazy'
fi

# Suppress Ruby warnings
export RUBYOPT=-W0

# 1Password CLI integration (if available)
if command -v op &>/dev/null; then
  export OP_BIOMETRIC_UNLOCK_ENABLED=true
fi

# BEETS ENV VARIABLE
if [[ -d "$HOME/.config/beets" ]]; then
  export BEETSDIR="$HOME/.config/beets/"
fi

# fnm fast node manager (cross-platform)
if command -v fnm &>/dev/null; then
  eval "$(fnm env --use-on-cd --shell zsh --version-file-strategy=recursive)"
fi

# zoxide (lazy-loaded for faster shell startup)
if command -v zoxide &>/dev/null; then
  _zoxide_lazy() {
    unalias z zi 2>/dev/null
    unset -f _zoxide_lazy
    eval "$(zoxide init zsh)"
    z "$@"
  }
  _zoxide_lazy_i() {
    unalias z zi 2>/dev/null
    unset -f _zoxide_lazy _zoxide_lazy_i
    eval "$(zoxide init zsh)"
    zi "$@"
  }
  alias z='_zoxide_lazy'
  alias zi='_zoxide_lazy_i'
fi

##########
# HISTORY
##########

HISTFILE=$HOME/.zsh_history
HISTSIZE=50000
SAVEHIST=50000

# Immediately append to history file:
setopt INC_APPEND_HISTORY

# Record timestamp in history:
setopt EXTENDED_HISTORY

# Expire duplicate entries first when trimming history:
setopt HIST_EXPIRE_DUPS_FIRST

# Dont record an entry that was just recorded again:
setopt HIST_IGNORE_DUPS

# Delete old recorded entry if new entry is a duplicate:
setopt HIST_IGNORE_ALL_DUPS

# Do not display a line previously found:
setopt HIST_FIND_NO_DUPS

# Dont record an entry starting with a space:
setopt HIST_IGNORE_SPACE

# Dont write duplicate entries in the history file:
setopt HIST_SAVE_NO_DUPS

# Share history between all sessions:
setopt SHARE_HISTORY

# Execute commands using history (e.g.: using !$) immediatel:
unsetopt HIST_VERIFY

# ============================================================================
# Machine-Specific Overrides
# ============================================================================
# Load machine-specific configuration if it exists
# This allows customization per-machine without modifying the main config
# Example: Work-specific aliases, different paths, custom functions, etc.
#
# Create ~/.zshrc.local with machine-specific settings (not tracked in git)
if [[ -f "$HOME/.zshrc.local" ]]; then
  source "$HOME/.zshrc.local"
fi
